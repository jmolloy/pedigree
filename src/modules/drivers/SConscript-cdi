####################################
# SCons build system for Pedigree
# CDI-specific build system integration
# Matthew Iselin
####################################

import os.path
Import(['env'])

cdi_drivers = env['cdi_driver_list']

# Meh. cdi-osdep.h is in both locations, this isn't ideal.
# TODO: Fix me!
include_dirs = [
    '#/src/system/include',
    '#/src/modules/drivers',
    '#/src/modules/drivers/common/cdi/include',
    '#/src/modules/drivers/cdi/include'
]

libraries = [
    'gcc'
]

# For each driver, handle its compilation
for driver in cdi_drivers:
    localenv = env.Clone()

    directory = "cdi/" + driver
    
    objname = '#' + env["PEDIGREE_BUILD_DRIVERS"]
    objname += '/' + driver + '.o'
    
    env.Alias("drivers", objname)
    
    driverFiles = [Glob(directory + '/*.c')]
    
    localenv['LIBS'] = libraries
    localenv['CPPPATH'] = include_dirs
    localenv['CPPPATH'] += [directory + '/include']

    # TOOD: do a warning fixup in CDI
    localenv['CFLAGS'] = localenv['CFLAGS'].replace('-Werror', '')
    localenv['CXXFLAGS'] = localenv['CXXFLAGS'].replace('-Werror', '')

    localenv.Program(objname, driverFiles, LINKFLAGS='-nostdlib -Wl,-r -T $LSCRIPT', LSCRIPT=File("#src/modules/link.ld"))


