#!/usr/bin/env python2.7
'''
Copyright (c) 2008-2014, Pedigree Developers

Please see the CONTRIB file in the root of the source tree for a full
list of contributors.

Permission to use, copy, modify, and distribute this software for any
purpose with or without fee is hereby granted, provided that the above
copyright notice and this permission notice appear in all copies.

THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
'''

import os

import buildutils.userspace

Import(['env'])

env_clone = env.Clone()

libsbase        = 'libraries'
appoutputdir    = env["PEDIGREE_BUILD_APPS"]
imagesdir       = env["PEDIGREE_IMAGES_DIR"]
builddir        = env["PEDIGREE_BUILD_BASE"]

# Libraries to compile
libs = [
    'libui',
]

# Subsystem for each set of libraries
subsys = {
    'libui' : 'native',
}

lib_deps = {
    'libui': ['pedigree'],
}

env_clone['CFLAGS'] = buildutils.userspace.fixFlags(env, env['CFLAGS'])
env_clone['CXXFLAGS'] = buildutils.userspace.fixFlags(env, env['CXXFLAGS'])
env_clone['CPPDEFINES'] += ['PEDIGREE', '__PEDIGREE__']
env_clone['LIBS'] = []
env_clone['LIBPATH'] = [builddir]
env_clone['CPPPATH'] = []

for library in libs:
    library_env = env_clone.Clone()
    
    # Determine the subsystem and include directories for this library
    incpath = []
    s = subsys[library]
    if s == 'native':
        cpp_base = '#/images/local/support/gcc/include/c++/%s' % (env['CCVERSION'],)
        incpath += ['#/src/subsys/native/include',
                    cpp_base,
                    '%s/%s' % (cpp_base, env['COMPILER_TARGET']),
                    ]
    else:
        print "** Library with subsys '%s' is not buildable - fix src/user/libraries/SConscript!" % (s)

    srcdir = env.Dir(library).srcnode().abspath

    # Handle extra include directories within the library
    custom_include_dir = os.path.join(srcdir, 'include')
    if(os.path.exists(custom_include_dir)):
        incpath.append(custom_include_dir)

    library_env['CPPPATH'] = incpath
    library_env['LIBS'] = lib_deps.get(library, [])

    output = os.path.join(builddir, 'libs', '%s.a' % library)
    shoutput = os.path.join(builddir, 'libs', '%s.so' % library)
    files = [Glob(os.path.join(library, 'src', '*.c')),
             Glob(os.path.join(library, 'src', '*.cc'))]

    library_env.StaticLibrary(output, files)
    library_env.SharedLibrary(shoutput, files, LINKFLAGS='')

    env.Alias("libs", output)
    env.Alias("libs", shoutput)

    # XXX: We have an unfortunate dependency on libc.
    libc_objname = os.path.join(builddir, 'libc.so')
    env.Depends(shoutput, libc_objname)
