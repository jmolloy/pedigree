'''
Copyright (c) 2008-2014, Pedigree Developers

Please see the CONTRIB file in the root of the source tree for a full
list of contributors.

Permission to use, copy, modify, and distribute this software for any
purpose with or without fee is hereby granted, provided that the above
copyright notice and this permission notice appear in all copies.

THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
'''


import os
import pipes

import buildutils.misc
import buildutils.userspace
import buildutils.fs
import buildutils.libs

Import(['env', 'userspace_env'])

env_clone = env.Clone()
userspace_env_clone = userspace_env.Clone()

builddir = env["PEDIGREE_BUILD_BASE"]
kerneldir = env["PEDIGREE_BUILD_KERNEL"]
moduledir = env["PEDIGREE_BUILD_MODULES"]
subsysdir = env["PEDIGREE_BUILD_SUBSYS"]
imagesdir = env["PEDIGREE_IMAGES_DIR"]

posix_objname = os.path.join(subsysdir, 'posix.o')

glue_objname = os.path.join(builddir, 'libpedigree-glue.a')

libpthread_objname = os.path.join(builddir, 'libpthread.a')
libpthread_shobjname = os.path.join(builddir, 'libpthread.so')

crt0_objname = os.path.join(kerneldir, 'crt0.o')
crti_objname = os.path.join(kerneldir, 'crti.o')
crtn_objname = os.path.join(kerneldir, 'crtn.o')

libpedigree_c_objname = os.path.join(builddir, 'libpedigree-c.a')
libpedigree_c_shobjname = os.path.join(builddir, 'libpedigree-c.so')

libc_objname = os.path.join(builddir, 'libc.so')

env.Alias("crt", [crt0_objname, crti_objname, crtn_objname])
env.Alias("subsys", posix_objname)
env.Alias("subsys", glue_objname)
env.Alias("pthreads", libpthread_objname)
env.Alias("pthreads", libpthread_shobjname)

env.Depends(libc_objname, "crt")
env.Depends(libc_objname, glue_objname)
env.Depends(libc_objname, libpedigree_c_objname)
env.Depends(libpedigree_c_shobjname, libc_objname)

# To include new files just add to the list, or Glob("*.cpp")
posix_files = [
    Glob("*.cc")
]

glue_files = [
    'glue.c',
    'glue-sysconf.c',
    'glue-fseek.c',
    'glue-fpurge.c',
    'glue-scandir.c',
    'glue-dlmalloc.c',
    'glue-strcasecmp.c',
    'glue-utmpx.c',
    'glue-infoblock.c',
    'glue-memset.c',
]

if 'CUSTOM_MEMCPY' in userspace_env['EXTRA_CONFIG']:
    glue_files.extend(['glue-memcpy.c'])

crt0_files = ['crt0.c']

suffix = buildutils.misc.stubSuffix(userspace_env)

posix_files += ["sigret" + suffix + ".s"]
glue_files += ["setjmp" + suffix + ".s"]

pthread_files = ['glue-pthread.c']

# CRTI/CRTN
crti_files = ["crti" + suffix + ".s"]
crtn_files = ["crtn" + suffix + ".s"]

# To include a new directory for includes, just add it to the list
include = [
    './syscalls',
    './include',
    '#/src/modules',
    '#/src/system/include',
    '#/src/modules/system',
    '#/src/subsys/pedigree-c',
    '.'
]

# To add a library path add to the list below
libpaths = [
	builddir
]

####################################
env_clone['CPPPATH'] = include
env_clone['LIBS'] = []
env_clone['LIBPATH'] = []

final_output = posix_objname
posix_objname = '%s.debug' % (posix_objname,)

if "STATIC_DRIVERS" in env['CPPDEFINES']:
    env_clone['LSCRIPT'] = File("#src/modules/link_static.ld")
else:
    env_clone['LSCRIPT'] = File("#src/modules/link.ld")

# Handle logging flags.
logging_mask = 0
if env_clone['posix_verbose']:
    logging_mask = 0xFFFFFFFF
else:
    if env_clone['posix_file_verbose']:
        logging_mask |= 1
    if env_clone['posix_sys_verbose']:
        logging_mask |= 2
    if env_clone['posix_thr_verbose']:
        logging_mask |= 4
    if env_clone['posix_net_verbose']:
        logging_mask |= 8
    if env_clone['posix_sig_verbose']:
        logging_mask |= 16
    if env_clone['posix_sig_ultra_verbose']:
        logging_mask |= 32

env_clone['CPPDEFINES'] += ['POSIX_LOG_FACILITIES=%d' % logging_mask]

env_clone['AS'] = userspace_env_clone['AS']
env_clone['ASFLAGS'] = userspace_env_clone['ASFLAGS']
env_clone.Program(posix_objname, posix_files, LINKFLAGS='-nostdlib -Wl,-r -Wl,-T,$LSCRIPT')
env_clone.Command(final_output, posix_objname, action="%s -d -o $TARGET $SOURCE" % env['STRIP'])

posix_env = userspace_env_clone.Clone()
posix_env['CPPPATH'] = include
posix_env['LIBPATH'] = libpaths
posix_env['CFLAGS'] = buildutils.userspace.fixFlags(userspace_env_clone, posix_env['CFLAGS'])
posix_env['CCFLAGS'] = buildutils.userspace.fixFlags(userspace_env_clone, posix_env['CCFLAGS'])
posix_env['CXXFLAGS'] = buildutils.userspace.fixFlags(userspace_env_clone, posix_env['CXXFLAGS'])

pic_clone = posix_env.Clone()
pic_clone.MergeFlags('-fPIC -DUSE_PIC_SYSCALLS')

# TODO: rip out stdint.h, make sure GCC target provides it.

pic_clone.Library(glue_objname, glue_files)

# No libraries needed for CRT0
pic_clone.Program(crt0_objname, crt0_files, LINKFLAGS='-nostdlib -Wl,-r')

# CRTI
pic_clone.Program(crti_objname, crti_files, LINKFLAGS='-nostdlib -Wl,-r')

# CRTN
pic_clone.Program(crtn_objname, crtn_files, LINKFLAGS='-nostdlib -Wl,-r')

# libpthread
env_clone_pthread = pic_clone.Clone()
env_clone_pthread.Library(libpthread_objname, pthread_files)
env_clone_pthread.SharedLibrary(libpthread_shobjname, pthread_files, LINKFLAGS='')

# Build libc/libm

buildDir = env_clone.Dir(builddir)

base = env_clone.Dir("#")
baseLibc = env_clone.File(builddir + "/stock-libg.a")
baseLibm = env_clone.File(builddir + "/stock-libm.a")
baseInclude = env_clone.Dir("#src/subsys/posix/include")

buildLibc = env_clone.File(builddir + '/libc.so')
buildLibm = env_clone.File(builddir + '/libm.so')
buildLibcA = env_clone.File(builddir + '/libc.a')
buildLibmA = env_clone.File(builddir + '/libm.a')
buildInclude = env_clone.Dir(builddir + '/include-posix')

env.Alias("crt0", "crt")

# Install updated headers to the local images directory. Newlib depends on
# them, rather than the provided-with-newlib headers.
buildutils.fs.install_tree(
    env.Dir(os.path.join(imagesdir, 'include')), baseInclude, env, 'headers')

# TODO(miselin): we really should be able to build libc on Pedigree. Fix this.
if not env['ON_PEDIGREE']:
    buildutils.libs.buildLibc(env_clone, baseLibc, glue_objname)
    buildutils.libs.buildLibm(env_clone, baseLibm)

    env.Alias("libs", [buildLibc, buildLibm])

    SConscript(os.path.join('newlib', 'SConscript'), exports=['userspace_env'])
