'''
Copyright (c) 2008-2014, Pedigree Developers

Please see the CONTRIB file in the root of the source tree for a full
list of contributors.

Permission to use, copy, modify, and distribute this software for any
purpose with or without fee is hereby granted, provided that the above
copyright notice and this permission notice appear in all copies.

THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
'''

import os

Import(['env'])

driverdir = env["PEDIGREE_BUILD_DRIVERS"]
moduledir = env["PEDIGREE_BUILD_MODULES"]
imagesdir = env["PEDIGREE_IMAGES_DIR"]

# Common drivers - order is important
driver_common_subdirs = [
    # DMA
    'dma',

    # Pedigree-specific disk I/O
    'ata',
    'partition',

    # Pedigree-specific video
    'nvidia',

    # Pedigree-specific NICs
    '3c90x',
    # 'rtl8139',
    'loopback',

    # Pedigree-specific SCSI layer
    'scsi',

    # Pedigree-specific HID layer
    'hid',

    # Pedigree-specific USB drivers
    'usb-hcd',
    'usb-hub',
    'usb-mass-storage',
    'usb-hid',

    # Generic FTDI driver (USB serial)
    'ftdi',

    # DM9601 driver (USB ethernet)
    'dm9601',

    # CDI framework (NOT the drivers)
    'cdi',
]

# CDI drivers
cdi_drivers = [
    # CDI NIC drivers
    'e1000',
    'pcnet',
    'rtl8139',
    'sis900',

    # CDI Disk drivers
    # 'floppy' # Needs millisecond granularity in cdi_sleep_ms...
]

# Architecture-specific drivers
if env['ARCH_TARGET'] in ('X86', 'X64'):
    driver_arch = 'x86'
    driver_arch_subdirs = [
        'ib700_wdt',
        'ne2k',
        'pci',
        'ps2mouse',
        'vbe',
        'vmware-gfx',
    ]
elif env['ARCH_TARGET'] == 'PPC':
    driver_arch = 'ppc'
    driver_arch_subdirs = [
        'ata-specific',
        'framebuffer',
        'test',
    ]
elif env['ARCH_TARGET'] == 'ARM':
    driver_arch = 'arm'
    driver_arch_subdirs = [
        'usb-glue',
    ]

    # Filter out useless drivers for ARM
    driver_common_subdirs = [x for x in driver_common_subdirs
                             if x not in ['ata', 'dma', 'cdi', 'nvidia', '3c90x']]
    cdi_drivers = []

env['cdi_driver_list'] = cdi_drivers

# Modules - order is important
module_subdirs = [
    'vfs',
    'ext2',
    'fat',
    'iso9660',
    'network-stack',
    'dhcpclient',
    'console',
    'TUI',
    'linker',
    'users',
    'ramfs',
    'rawfs',
    'lodisk',
    'usb',
    'splash',
    'config',
    'status_server',
    'gfx-deps',
    'preload'
]

if env['installer']:
    module_subdirs += ['installer']
else:
    module_subdirs += ['init']

if env['ARCH_TARGET'] == 'ARM':
    module_subdirs = [x for x in module_subdirs if x not in ['splash', 'TUI']]

# No difference yet, load all modules
initrd_modules = module_subdirs

drivers = [os.path.join('drivers', driver_arch, i) for i in driver_arch_subdirs]
drivers += [os.path.join('drivers', 'common', i) for i in driver_common_subdirs]
modules = [os.path.join('system', i) for i in module_subdirs]

all_modules = drivers + modules

# Custom CFLAGS for specific modules
custom_cflags = {
    'config' : ['-DSQLITE_OS_OTHER=1',
                '-DSQLITE_THREADSAFE=0',
                '-DSQLITE_TEMP_STORE=3',
                '-DSQLITE_DISABLE_LFS=1',
                '-DSQLITE_OMIT_LOCALTIME=1',
                '-DSQLITE_OMIT_FLOATING_POINT=1']
}

# Custom CXXFLAGS for specific modules
custom_cxxflags = {
}

# Flags from CXXFLAGS and CFLAGS to *remove* for a module
remove_flags = {
    # We always remove -Werror from the config module as sqlite is noisy.
    'config' : ['-ffreestanding', '-fno-builtin', '-Werror'],
    # CDI drivers
    'e1000': ['-Werror'],
    'pcnet': ['-Werror'],
    'rtl8139': ['-Werror'],
}

# Try and follow this subdirectory scheme for platform code in modules to
# simplify the custom_subdirs dictionary
arch_subdir = ''
if env['ARCH_TARGET'] == 'X86':
    arch_subdir = 'i686'
elif env['ARCH_TARGET'] == 'X64':
    arch_subdir = 'amd64'
elif env['ARCH_TARGET'] == 'ARM':
    arch_subdir = 'arm'
elif env['ARCH_TARGET'] == 'PPC':
    arch_subdir = 'ppc'
else:
    arch_subdir = 'unknown'

def dma_fix(s):
    if s in ('X86', 'X64'):
        return 'X86'
    else:
        return s

# Custom subdirectories for specific modules
custom_subdirs = {
    'dma'           : [dma_fix(env['ARCH_TARGET']).lower()],
    'linker'        : [arch_subdir]
}

# Custom files to add to the file list
custom_files = {
    'linker'        : [os.path.join(arch_subdir, 'asm-' + arch_subdir + '.s')]
}

# Custom include paths for specific modules
custom_incpath = {
    'cdi'           : ['#/src/modules/drivers/common/cdi/include',
                       '#/src/modules/drivers/cdi/include'],
    'status_server' : ['#/src/system/kernel'],
    'TUI'           : ['#/src/system/kernel'],
    'init'          : ['#/src/system/kernel', '#/src'],
    'splash'        : ['#/src/system/kernel'], # For nogfx
    'linker'        : ['#/src/modules/system/linker'],
    'usb-glue'      : ['#/src/system/kernel/machine/arm_beagle'] # TODO: handle other machine types
}

# Custom library paths for specific modules
custom_libs = {
}

# Default library paths
default_libpath = [
    imagesdir + '/libraries',
]

# Default include paths
default_incpath = [
    '#/src/modules',
    '#/src/system/include',
    '#/src/modules/system',
    '#/src/modules/drivers/common',
]

env_clone = env.Clone()
env_clone['LIBS'] = []
env_clone['LIBPATH'] = default_libpath
env_clone['CPPPATH'] = default_incpath

def buildModule(env, path, outputBase, targetAlias):
    module_env = env.Clone()

    # Handle extra include directories
    base = path
    module = os.path.basename(path)

    custom_include_path = os.path.join(base, 'include')
    if(os.path.exists(custom_include_path)):
        module_env['CPPPATH'] += [custom_include_path]

    module_env['CPPPATH'] += custom_incpath.get(module, [])
    module_env['LIBS'] += custom_libs.get(module, [])
    module_env['CFLAGS'] += ' ' + ' '.join(custom_cflags.get(module, ''))
    module_env['CXXFLAGS'] += ' ' + ' '.join(custom_cxxflags.get(module, ''))
    for flag in remove_flags.get(module, []):
        module_env['CFLAGS'] = module_env['CFLAGS'].replace(flag, ' ')
        module_env['CXXFLAGS'] = module_env['CXXFLAGS'].replace(flag, ' ')
    
    output = os.path.join(outputBase, module + '.o')
    files = [Glob(os.path.join(base, '*.c')), Glob(os.path.join(base, '*.cc'))]
    for d in custom_subdirs.get(module, []):
        files += [
            Glob(os.path.join(base, d, '*.c')),
            Glob(os.path.join(base, d, '*.cc')),
        ]
    for f in custom_files.get(module, []):
        files += [os.path.join(base, f)]

    final_output = output
    unstripped_temporary = output = '%s.debug' % (output,)
    
    if "STATIC_DRIVERS" in env['CPPDEFINES']:
        module_env.Program(unstripped_temporary, files,
            LINKFLAGS='-nostdlib -Wl,-r -T $LSCRIPT',
            LSCRIPT=File("#src/modules/link_static.ld"))
        module_env.Command(final_output, unstripped_temporary,
            action="%s -d -o $TARGET $SOURCE" % env['STRIP'])
    else:
        module_env.Program(unstripped_temporary, files,
            LINKFLAGS='-nostdlib -Wl,-r -T $LSCRIPT',
            LSCRIPT=File("#src/modules/link.ld"))
        module_env.Command(final_output, unstripped_temporary,
            action="%s -d -o $TARGET $SOURCE" % env['STRIP'])
    
    env.Alias(targetAlias, final_output)

for module in modules:
    buildModule(env_clone, module, moduledir, 'modules')

for driver in drivers:
    buildModule(env_clone, driver, driverdir, 'drivers')

# Special handling for CDI drivers
env_clone['CPPPATH'] += ['#/src/modules/drivers/common/cdi/include',
                         '#/src/modules/drivers/cdi/include']

for cdi_driver in cdi_drivers:
    cdi_env = env_clone.Clone()

    # Patch up the path and add any include directory to the path
    cdi_driver = os.path.join('drivers', 'cdi', cdi_driver)
    incdir = env.Dir(os.path.join('#src', 'modules', cdi_driver, 'include'))
    if os.path.exists(incdir.abspath):
        cdi_env['CPPPATH'] += [incdir]
    buildModule(cdi_env, cdi_driver, driverdir, 'drivers')

# Ready to generate the initrd...
builddir = env.Dir(env["PEDIGREE_BUILD_BASE"])
initrdFile = env.File(os.path.join(env["PEDIGREE_BUILD_BASE"], "initrd.tar"))

# initrd lists

# Ensure graphics drivers are loaded before splash
initrdList = []
forceModuleOrder = ['config', 'pci', 'vbe', 'vmware-gfx', 'gfx-deps', 'splash']
all_drivers = driver_arch_subdirs + driver_common_subdirs + cdi_drivers
for module in forceModuleOrder:
    if module in initrd_modules:
        initrdList += [os.path.join(builddir.abspath, 'modules', module + '.o')]
        initrd_modules.remove(module)
    elif module in all_drivers:
        initrdList += [os.path.join(builddir.abspath, 'drivers', module + '.o')]
        all_drivers.remove(module)

# Add all drivers.
# TODO: Installation should detect hardware and install a custom initrd with
# 		only the necessary drivers to boot up (and then drivers should be
#		loaded on-demand or something)
initrdList += [os.path.join(builddir.abspath, 'drivers', i + '.o') for i in all_drivers]

# Only some modules are actually needed for the actual boot of the system
# The rest can be loaded via some form of autoload mechanism (run in the init
# module perhaps?) or on demand via modload.
initrdList += [os.path.join(builddir.abspath, 'modules', i + '.o') for i in initrd_modules]

# Subsystems
initrdList += [os.path.join(builddir.abspath, 'subsystems', 'posix.o')]
initrdList += [os.path.join(builddir.abspath, 'subsystems', 'pedigree-c.o')]
if env['ARCH_TARGET'] in ('X86', 'X64'):
    initrdList += [os.path.join(builddir.abspath, 'subsystems', 'native.o')]

env.Depends(initrdFile, "drivers")
env.Depends(initrdFile, "modules")
env.Depends(initrdFile, "subsys")

env.Alias("initrd", initrdFile)

if('STATIC_DRIVERS' in env['CPPDEFINES']):
    env.Depends("kernel", initrdList) # Kernel depends on all drivers/modules
    env['INITRD_LIST'] = initrdList
else:
    env.CreateTar(initrdFile, initrdList)
