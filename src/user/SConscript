#!/usr/bin/env python2.7
'''
Copyright (c) 2008-2014, Pedigree Developers

Please see the CONTRIB file in the root of the source tree for a full
list of contributors.

Permission to use, copy, modify, and distribute this software for any
purpose with or without fee is hereby granted, provided that the above
copyright notice and this permission notice appear in all copies.

THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
'''

import os

import buildutils.userspace
import buildutils.misc

Import(['userspace_env'])

env = userspace_env

env_clone = env.Clone()

appsbase = 'applications'
appoutputdir = env["PEDIGREE_BUILD_APPS"]
imagesdir = env["PEDIGREE_IMAGES_DIR"]
builddir = env["PEDIGREE_BUILD_BASE"]

suffix = buildutils.misc.stubSuffix(userspace_env)

EXTERNAL_LIBRARY_DIR = '#images/local/libraries'
EXTERNAL_INCLUDE_DIR = '#images/local/include'
if env['ON_PEDIGREE']:
    EXTERNAL_LIBRARY_DIR = '/libraries'
    EXTERNAL_INCLUDE_DIR = '/include'

# libloader (userspace dynamic linker).
libload_sources = [
    'loader.cc',
    'stubs%s.s' % suffix,
]

# Applications which just use POSIX libc/libm.
posixc_apps = [
    'crashtest',
    'display-mode',
    'gears',
    'init',
    'keymap',
    'login',
    'modlist',
    'modload',
    'modunload',
    'mount',
    'net-test',
    'nyancat',
    'preloadd',
    'reboot',
    'sudo',
    'syscall-test',
    'testsuite',
]

# Applications which use Mesa (affects libraries & headers).
mesa_apps = [
    'gears',
]

# Applications which use libSDL (affects libraries & headers).
sdl_apps = [
    'fire'
]

# Applications which use libpedigree and/or libstdc++.
cpp_apps = [
    'apptest',
    'config',
    'ipc-test-client',
    'ipc-test-server',
    'thread-test',
    'ttyterm',
    'TUI',
    'uitest',
]

# Applications which require C++11.
cpp_11_apps = [
    'which',
    'winman',
    'tour',
]

# Applications which use libui (affects libraries & headers).
libui_apps = [
    'gears',
    'TUI',
    'uitest',
]

# Applications which need pango for text rendering.
pango_apps = [
    'winman',
    'TUI',
]

# Custom include paths for specific applications.
custom_incpath = {
    'TUI' : [EXTERNAL_INCLUDE_DIR,
             EXTERNAL_INCLUDE_DIR + '/freetype2'],
    'winman' : ['#src/user/libraries/libui/include',
                '#src/user/libraries/libui/src',
                EXTERNAL_INCLUDE_DIR,
                EXTERNAL_INCLUDE_DIR + '/pixman-1',
                EXTERNAL_INCLUDE_DIR + '/freetype2']
}

# Custom libraries for specific applications
custom_libs = {
    'config': ['pedigree'],
    'ipc-test-client': ['pedigree'],
    'ipc-test-server': ['pedigree'],
    'thread-test' : ['pthread'],
    'ttyterm': ['pedigree'],
    'TUI' : ['cairo', 'freetype', 'png', 'iconv', 'pedigree'],
    'uitest' : ['pedigree'],
    'winman': ['cairo', 'freetype', 'png', 'pedigree'],
    'apptest': ['pedigree'],
}

# Applications to not enable -Werror for.
no_werror_apps = [
    'fire',  # C89.
    'gears',  # opengl
    'keymap',  # lex/yacc
    'nyancat',  # not ours
]

# Default libraries for all applications
base_libs = ['pthread']

# Default include paths
base_include_dirs = [
    EXTERNAL_INCLUDE_DIR,
]

# Flags from CXXFLAGS and CFLAGS to *remove* for an app
remove_flags = {
    'nyancat': ['-Wcast-qual'],
    'which': ['-Wwrite-strings'],
    'tour': ['-Wwrite-strings'],
}

# Extra flags to add to flags for an app.
add_flags = {
    # Both of these use getopt, which has a const-ness problem.
    'nyancat': ['-Wno-cast-qual', '-Wno-write-strings', '-Wno-clobbered'],
    'which': ['-Wno-cast-qual', '-Wno-write-strings'],
    'tour': ['-Wno-write-strings'],
}

env_clone['CFLAGS'] = buildutils.userspace.fixFlags(env, env['CFLAGS'])
env_clone['CCFLAGS'] = buildutils.userspace.fixFlags(env, env['CCFLAGS'])
env_clone['CXXFLAGS'] = buildutils.userspace.fixFlags(env, env['CXXFLAGS'])
env_clone['CPPDEFINES'] += ['PEDIGREE', '__PEDIGREE__']
env_clone['LIBS'] = base_libs
env_clone['LIBPATH'] = [builddir, os.path.join(builddir, 'libs')]
env_clone['CPPPATH'] = base_include_dirs

# -static stops GCC placing the path to the dynamic linker into the binary, and creates a non-relocateable executable.
libload_env = env_clone.Clone()
libload_env.MergeFlags('-static')

libload_env['CPPPATH'] += ['#src/system/include/linker']
if not env['ON_PEDIGREE']:
    cpp_base = '#images/local/support/gcc/include/c++/%s' % env['CCVERSION']
    cpp_arch_base = os.path.join(cpp_base, env['COMPILER_TARGET'])
    libload_env['CPPPATH'] += [cpp_base, cpp_arch_base]

libload_env['LIBPATH'] += [builddir, imagesdir + 'libraries']
libload_env['LIBS'] = ['-lstdc++', '-lc', '-lpedigree', '-lpedigree-c', '-lgcc']

libload = os.path.join(builddir, 'libload.so')
libload_linkscript = libload_env.File(os.path.join(appsbase, 'libload', 'libload%s.ld' % suffix))

in_files = [os.path.join(appsbase, 'libload', x) for x in libload_sources]
libload_env.Program(libload, in_files,
    LINKFLAGS="-static -Wl,-Bstatic -Wl,--no-whole-archive -nodefaultlibs -T%s " % libload_linkscript)
libload_env.Depends(libload, "crt0")

env.Alias('libs', libload)

for app in set(posixc_apps + mesa_apps + sdl_apps + cpp_apps + cpp_11_apps + libui_apps):
    app_env = env_clone.Clone()
    if env['hosted']:
        app_env['LSCRIPT'] = app_env.File(os.path.join('#src/system/kernel/core/processor/hosted/app.ld'))
        app_env['LINKFLAGS'] = '-T$LSCRIPT'
    else:
        app_env['LINKFLAGS'] = ''

    base = os.path.join(appsbase, app)

    # Handle custom paths and libraries.
    custom_include_path = os.path.join(base, 'include')
    if os.path.exists(custom_include_path):
        app_env['CPPPATH'] += [custom_include_path]

    custom_include_path = custom_incpath.get(app)
    if custom_include_path:
        app_env['CPPPATH'] += custom_include_path

    custom_link_libs = custom_libs.get(app)
    if custom_link_libs:
        app_env['LIBS'] += custom_link_libs

    # Handle SDL dependencies.
    if app in sdl_apps:
        # Don't build SDL apps if we don't have SDL.
        if not env['build_lgpl']:
            continue
        app_env['LIBS'] += ['SDL']
        app_env['CPPPATH'] += ['#src/lgpl/SDL-1.2.14/include',
                               EXTERNAL_INCLUDE_DIR]

    # Handle Mesa dependencies.
    if app in mesa_apps:
        app_env['LIBS'] += ['OSMesa']
        app_env['LIBPATH'] += [EXTERNAL_LIBRARY_DIR]
        app_env['CPPPATH'] += [EXTERNAL_INCLUDE_DIR]

    # Handle C++-related dependencies.
    if app in set(cpp_apps + sdl_apps + mesa_apps + cpp_11_apps):
        app_env['LIBPATH'] += [EXTERNAL_LIBRARY_DIR]
        if not env['ON_PEDIGREE']:
            app_env['CPPPATH'] += [cpp_base, cpp_arch_base]
        app_env['LINK'] = env['CXX']

    # Handle libui-related dependencies.
    if app in libui_apps:
        app_env['CPPPATH'] += ['#src/user/libraries/libui/include']
        app_env['LIBS'] += ['ui']

    if app in pango_apps:
        app_env['LIBPATH'] += [EXTERNAL_LIBRARY_DIR]
        app_env['CPPPATH'] += [
            EXTERNAL_LIBRARY_DIR + '/glib-2.0/include',
            EXTERNAL_INCLUDE_DIR + '/glib-2.0',
            EXTERNAL_INCLUDE_DIR + '/pango-1.0',
            EXTERNAL_INCLUDE_DIR + '/cairo',
        ]

        # pango pulls in a lot of dependencies.
        app_env['LIBS'] += ['cairo', 'pangocairo-1.0', 'pangoft2-1.0',
            'pango-1.0', 'harfbuzz', 'fontconfig', 'gobject-2.0', 'glib-2.0',
            'ffi']

    app_env['LINKFLAGS'] += ' -Wl,-rpath-link -Wl,%s ' % (
        app_env.Dir(builddir).abspath,)
    app_env['LINKFLAGS'] += ' -Wl,-rpath-link -Wl,%s/libs/ ' % (
        app_env.Dir(builddir).abspath,)
    app_env['LINKFLAGS'] += ' -Wl,-rpath-link -Wl,%s/libraries/ ' % (
        app_env.Dir(imagesdir).abspath,)

    if app in cpp_11_apps:
        try:
            app_env['CXXFLAGS'].remove('-std=gnu++98')
        except ValueError:
            pass  # Not found.
        app_env.MergeFlags({'CXXFLAGS': '-std=gnu++11'})

    buildutils.misc.removeFromAllFlags(app_env, set(remove_flags.get(app, [])))

    if app in no_werror_apps:
        buildutils.misc.removeFromAllFlags(app_env, '-Werror')

    app_env.MergeFlags({'CCFLAGS': add_flags.get(app, [])})

    output = os.path.join(builddir, 'apps', app)
    files = [Glob(os.path.join(base, '*.c')), Glob(os.path.join(base, '*.cc'))]

    app_env.Program(output, files)

    env.Alias("apps", output)

    if 'LSCRIPT' in app_env:
        env.Depends(output, app_env['LSCRIPT'])

    env.Depends(output, "crt0")
    env.Depends(output, "libs")
    env.Depends(output, "headers")

SConscript(os.path.join('libraries', 'SConscript'), exports=['env'])
