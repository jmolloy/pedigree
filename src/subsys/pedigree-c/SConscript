'''
Copyright (c) 2008-2014, Pedigree Developers

Please see the CONTRIB file in the root of the source tree for a full
list of contributors.

Permission to use, copy, modify, and distribute this software for any
purpose with or without fee is hereby granted, provided that the above
copyright notice and this permission notice appear in all copies.

THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
'''

import os

import buildutils.userspace
import buildutils.modules

Import(['env', 'userspace_env'])

env_clone = env.Clone()
userspace_env_clone = userspace_env.Clone()

builddir = env["PEDIGREE_BUILD_BASE"]
kerneldir = env["PEDIGREE_BUILD_KERNEL"]
moduledir = env["PEDIGREE_BUILD_MODULES"]
subsysdir = env["PEDIGREE_BUILD_SUBSYS"]

pedigree_c_objname = os.path.join(subsysdir, 'pedigree-c.o')
libpedigree_c_objname = os.path.join(builddir, 'libpedigree-c.a')
libpedigree_c_shobjname = os.path.join(builddir, 'libpedigree-c.so')

env.Alias("subsys", pedigree_c_objname)

kernel_module_files = Glob("*.cc")

libpedigree_c_files = Glob("*.c")

# To include a new directory for includes, just add it to the list
include_dirs = [
    './include',
    '#src/modules',
    '#src/system/include',
    '#src/modules/system',
    '#images/local/include',
    '.'
]

env_clone['CPPPATH'] = include_dirs
env_clone['LIBS'] = []
env_clone['LIBPATH'] = []

final_output = pedigree_c_objname
pedigree_c_objname = '%s.debug' % (pedigree_c_objname,)

buildutils.modules.buildModule(env_clone, final_output, pedigree_c_objname,
                               kernel_module_files)

libs_clone = userspace_env_clone.Clone()
libs_clone.MergeFlags('-fPIC -DUSE_PIC_SYSCALLS')
libs_clone['CPPPATH'] = include_dirs
libs_clone['LIBPATH'] = [builddir]

libs_clone['CFLAGS'] = buildutils.userspace.fixFlags(userspace_env, libs_clone['CFLAGS'])
libs_clone['CCFLAGS'] = buildutils.userspace.fixFlags(userspace_env, libs_clone['CCFLAGS'])
libs_clone['CXXFLAGS'] = buildutils.userspace.fixFlags(userspace_env, libs_clone['CXXFLAGS'])

if env['build_libs']:
    env.Alias("subsys", libpedigree_c_objname)
    env.Alias("subsys", libpedigree_c_shobjname)

    libs_clone.StaticLibrary(libpedigree_c_objname, libpedigree_c_files)
    libs_clone.SharedLibrary(libpedigree_c_shobjname, libpedigree_c_files, LINKFLAGS='')

    env.Depends(libpedigree_c_objname, "crt")
    env.Depends(libpedigree_c_objname, "headers")

    env.Depends(libpedigree_c_shobjname, "crt")
    env.Depends(libpedigree_c_shobjname, "headers")
