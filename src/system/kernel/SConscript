'''
Copyright (c) 2008-2014, Pedigree Developers

Please see the CONTRIB file in the root of the source tree for a full
list of contributors.

Permission to use, copy, modify, and distribute this software for any
purpose with or without fee is hereby granted, provided that the above
copyright notice and this permission notice appear in all copies.

THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
'''

import os

Import(['env'])

# Take a copy of the SCons environment as we tweak flags.
env_clone = env.Clone()

kerneldir = env["PEDIGREE_BUILD_KERNEL"]
imagesdir = env["PEDIGREE_IMAGES_DIR"]
builddir = env["PEDIGREE_BUILD_BASE"]
objname = os.path.join(kerneldir, 'kernel')

env.Alias("kernel", objname)

# Instrument the kernel.
if env['instrumentation']:
    env_clone.MergeFlags({
        'CCFLAGS': ['-finstrument-functions'],
        'CPPDEFINES': ['INSTRUMENTATION'],
    })

# Base set of kernel files, common to all architectures.
files = [
    'Log.cc',
    'SerialLog.cc',
    os.path.join(builddir, 'Version.cc'),
    'Archive.cc',
    'Spinlock.cc',
    'Subsystem.cc',
    'ServiceManager.cc',
    'Service.cc',
    'syscallError.cc',
    Glob('utilities/*.cc'),
    Glob('utilities/sha1/*.cc'),
    Glob('utilities/md5/*.cc'),
    Glob('machine/*.cc'),
    Glob('linker/*.cc'),
    Glob('core/*.cc'),
    Glob('core/process/*.cc'),
    Glob('core/processor/*.cc'),
    Glob('core/lib/*.cc'),
    Glob('core/lib/*.c'),
    Glob('config/*.cc'),
    Glob('network/*.cc'),
    Glob('graphics/*.cc'),
    Glob('time/*.cc'),
    # Always pull in libudis86, even if we don't build the rest of the debugger
    Glob('debugger/libudis86/*.c'),
]

# Static drivers build all drivers directly into the kernel rather than building
# them as actual modules.
if 'STATIC_DRIVERS' in env['CPPDEFINES']:
    files += env['INITRD_LIST']

# The debugger creates its own set of unique dependencies that must be handled.
if env['debugger']:
    files += [
        Glob('debugger/*.cc'),
        Glob('debugger/commands/*.cc'),
    ]
else:
    files += [
        'debugger/panic.cc',
        'debugger/SerialIO.cc',
        'debugger/LocalIO.cc',
        'debugger/DebuggerIO.cc',
    ]

include_dirs = [
    '#/src/system/include',
    '#/src/modules/system',
    '#/src/modules',
    'debugger/',
    'debugger/commands/',
    'debugger/libudis86/',
    'linker/',
    'core/',
    'core/lib/',
    '.'
]

# Pull in machine implementation.
files += [
    env.Dir('machine/$MACH_DIR').glob('*.cc'),
    env.Dir('machine/$MACH_DIR').glob('*.c'),
]

# Pull in headers for machine implementation.
include_dirs += [
    env.Dir('machine/$MACH_DIR'),
]

# Add x86 emulator (for real-mode BIOS calls).
if env['ARCH_TARGET'] in ['X86', 'X64']:
    files += [
        'machine/mach_pc/x86emu/debug.c',
        'machine/mach_pc/x86emu/decode.c',
        'machine/mach_pc/x86emu/fpu.c',
        'machine/mach_pc/x86emu/ops.c',
        'machine/mach_pc/x86emu/ops2.c',
        'machine/mach_pc/x86emu/prim_ops.c',
        'machine/mach_pc/x86emu/sys.c',
    ]

    include_dirs += [
        'machine/mach_pc/x86_emu/x86emu',
    ]
elif env['ARCH_TARGET'] in ['HOSTED']:
    # HOSTED pulls in the X86 disassembler.
    files += ['core/processor/x86_common/Disassembler.cc']

# Pull in architecture implementation.
files += [
    env.Dir('core/processor/$ARCH_DIR').glob('*.cc'),
    env.Dir('core/processor/$ARCH_DIR').glob('*.c'),
    env.Dir('core/processor/$ARCH_DIR/asm').glob('*.s'),
    env.Dir('core/processor/$ARCH_DIR/asm').glob('*.S'),
]

# Pull in a sub-architecture implementation if it exists.
if env['SUBARCH_DIR']:
    files += [
        env.Dir('core/processor/$SUBARCH_DIR').glob('*.cc'),
        env.Dir('core/processor/$SUBARCH_DIR').glob('*.c'),
        env.Dir('core/processor/$SUBARCH_DIR/asm').glob('*.s'),
        env.Dir('core/processor/$SUBARCH_DIR/asm').glob('*.S'),
    ]

# Set flags in the cloned environment for the build.
env_clone['CPPPATH'] = include_dirs
env_clone['LIBS'] = ['gcc']
if not env['hosted']:  # Don't let the kernel link against Pedigree .so objects
    env_clone['LIBPATH'] = ['#', os.path.join(imagesdir, 'libraries')]

# Fix up flags for hosted builds.
if env['hosted']:
    env_clone['LSCRIPT'] = env.File('#src/system/kernel/core/processor/hosted/kernel.ld')
    if env['clang']:
        wraps = ('strchr',)
    else:
        wraps = ('malloc', 'realloc', 'free', 'strchr')
    for wrap in wraps:
        env_clone['LINKFLAGS'] += ['-Wl,-wrap=%s' % wrap]
    env_clone['LINKFLAGS'] += ['-Wl,-T,$LSCRIPT']
    env_clone['LIBS'] += ['rt', 'dl']

final_output = objname
stripped_objname = final_output
objname = '%s.debug' % objname

if 'LSCRIPT' in env_clone:
    env_clone.Depends(objname, env_clone['LSCRIPT'])

if env['ARCH_TARGET'] == 'X64':
    # On x86_64, we objcopy a stripped binary into the final kernel binary, so
    # we need to have an intermediate file that gets stripped.
    stripped_objname = '%s.stripped' % (objname,)

env_clone.Program(objname, files)
env_clone.Command(stripped_objname, objname,
                  action="$STRIP -d -o $TARGET $SOURCE")

if final_output != stripped_objname:
    action = "$OBJCOPY -I elf64-x86-64 -O elf32-i386 $SOURCE $TARGET"
    env_clone.Command(final_output, stripped_objname, action=action)
