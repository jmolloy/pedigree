#!/usr/bin/env python2.7
'''
Copyright (c) 2008-2014, Pedigree Developers

Please see the CONTRIB file in the root of the source tree for a full
list of contributors.

Permission to use, copy, modify, and distribute this software for any
purpose with or without fee is hereby granted, provided that the above
copyright notice and this permission notice appear in all copies.

THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
'''

import os

import buildutils.userspace

Import(['env'])

env_clone = env.Clone()

builddir = env["PEDIGREE_BUILD_BASE"]
imagesdir = env["PEDIGREE_IMAGES_DIR"]
objname = os.path.join(builddir, 'libSDL.a')
sobjname = os.path.join(builddir, 'libSDL.so')

EXTERNAL_INCLUDE_DIR = '#images/local/include'
if env['ON_PEDIGREE']:
    EXTERNAL_INCLUDE_DIR = '/include'

files = [
    Glob(os.path.join('src', '*.c')),
    Glob(os.path.join('src', 'audio', '*.c')),
    Glob(os.path.join('src', 'cdrom', '*.c')),
    Glob(os.path.join('src', 'cpuinfo', '*.c')),
    Glob(os.path.join('src', 'events', '*.c')),
    Glob(os.path.join('src', 'file', '*.c')),
    Glob(os.path.join('src', 'joystick', '*.c')),
    Glob(os.path.join('src', 'stdlib', '*.c')),
    Glob(os.path.join('src', 'thread', '*.c')),
    Glob(os.path.join('src', 'timer', '*.c')),
    Glob(os.path.join('src', 'video', '*.c')),
    Glob(os.path.join('src', 'audio/dummy', '*.c')),
    Glob(os.path.join('src', 'video/pedigree', '*.c')),
    Glob(os.path.join('src', 'video/pedigree', '*.cc')),
    Glob(os.path.join('src', 'joystick/dummy', '*.c')),
    Glob(os.path.join('src', 'cdrom/dummy', '*.c')),
    Glob(os.path.join('src', 'thread/pthread', '*.c')),
    Glob(os.path.join('src', 'timer/unix', '*.c')),
    Glob(os.path.join('src', 'loadso/dlopen', '*.c')),
]

if env['ARCH_TARGET'] in ['X86', 'X64']:
    files.append(Glob(os.path.join('src', 'hermes', '*.asm')))

# To include a new directory for includes, just add it to the list
include = [
    './include',
    '#src/user/libraries/libui/include',
    EXTERNAL_INCLUDE_DIR,
    EXTERNAL_INCLUDE_DIR + '/pixman-1',
    EXTERNAL_INCLUDE_DIR + '/freetype2',
]

cppinclude = include
if not env['ON_PEDIGREE']:
    cpp_base = '#images/local/support/gcc/include/c++/%s' % env['CCVERSION']
    cppinclude += [
        cpp_base,
        os.path.join(cpp_base, env['COMPILER_TARGET']),
    ]

libraries = [
    'gcc',
    'c',
    'm',
    'ui',
    'pedigree',
    'pedigree-c',
    'pthread',
    'stdc++',
    'cairo',
    'pixman-1',
    'fontconfig',
    'expat',
    'freetype',
    'png',
    'z',
    'iconv',
]

# To add a library path add to the list below
libpaths = [
    '#',
    os.path.join(imagesdir, 'libraries'),
    builddir,
    os.path.join(builddir, 'libs'),
]

env_clone['CFLAGS'] = buildutils.userspace.fixFlags(env, env_clone['CFLAGS'])
env_clone['CCFLAGS'] = buildutils.userspace.fixFlags(env, env_clone['CCFLAGS'])
env_clone['CXXFLAGS'] = buildutils.userspace.fixFlags(env, env_clone['CXXFLAGS'])
env_clone['CPPPATH'] = cppinclude
env_clone['LIBS'] = libraries
env_clone['LIBPATH'] = libpaths
env_clone['CPPDEFINES'] += ['PEDIGREE']
env_clone['ASFLAGS'] += ['-I%s/' % env.Dir('#src/lgpl/SDL-1.2.14/src/hermes').path]

# Remove -Werror as SDL is an external package.
# Also remove some noisy warnings that we won't fix because this is an external
# package.
buildutils.misc.removeFromAllFlags(env_clone, ['-Werror', '-Wundef'])

obj = env_clone.Library(objname, files)
shobj = env_clone.SharedLibrary(sobjname, files, LINKFLAGS='')
env.Alias('libs', obj)
env.Alias('libs', shobj)
